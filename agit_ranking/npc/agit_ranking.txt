//===== eAthena Script ===================================================
//= Agit Points Ranking
//===== By: ==============================================================
//= Brian
//===== Current Version: =================================================
//= 1.0
//===== Compatible With: =================================================
//= rAthena
//===== Description: =====================================================
//= Every time WoE Ends, the guild will receive the so called 'Agit Points'.
//  It can be attained thru conquering castle. 
//  for example. Guild One has conquered the Swanhild Castle after the WoE ends, 
//  guild one receives 2 Agit points. Castles will have corresponding points.
//===== Additional Comments: =============================================
//= <>
//===== Suppot Topic: ====================================================
//= http://rathena.org/board/topic/78562-agit-points/?hl=%2Bagit+%2Bpoints
//========================================================================

/*
ALTER TABLE `guild` ADD `agit_points` SMALLINT(6) UNSIGNED NOT NULL DEFAULT '0';
*/


prontera,146,190,4	script	Guild Ranking::GrankLadder	405,{
	
	mes "[^990000Guild Ranking^000000]";
	if (query_sql("SELECT `name`,agit_points FROM guild WHERE agit_points ORDER BY agit_points DESC LIMIT 128", .@guild_name$,.@agit_points) == 0) {
		mes "The list is empty.";
	} else {
		for (set .@i,0; .@i<getarraysize(.@guild_name$); set .@i,.@i+1)
			mes (.@i+1)+".^000099 "+.@guild_name$[.@i]+"^000000 (^009900"+.@agit_points[.@i]+" "+( (.@agit_points[.@i]==1) ? "point" : "points" )+"^000000)";
	}
	close2;
	cutin "",255;
	end;

OnAgitEnd:
	sleep 500; // make sure WoE-ending scripts finished triggering
	query_sql "SELECT castle_id,guild_id FROM guild_castle WHERE guild_id>0 AND castle_id BETWEEN 0 AND 23", .@castle_id,.@guild_id;
	for (set .@i,0; .@i<getarraysize(.@guild_id); set .@i,.@i+1)
		query_sql "UPDATE guild SET agit_points = agit_points+"+.castle_points[.@castle_id[.@i]]+" WHERE guild_id="+.@guild_id[.@i];
	end;
OnAgitEnd2:
	sleep 500; // make sure WoE-ending scripts finished triggering
	query_sql "SELECT castle_id,guild_id FROM guild_castle WHERE guild_id>0 AND castle_id BETWEEN 24 AND 33", .@castle_id,.@guild_id;
	for (set .@i,0; .@i<getarraysize(.@guild_id); set .@i,.@i+1)
		query_sql "UPDATE guild SET agit_points = agit_points+"+.castle_points[.@castle_id[.@i]]+" WHERE guild_id="+.@guild_id[.@i];
	end;

OnInit:
	setarray .castle_points[0], 
	// Aldebaran
		1, // 0-aldeg_cas01, Neuschwanstein
		1, // 1-aldeg_cas02, Hohenschwangau
		1, // 2-aldeg_cas03, Nuernberg
		1, // 3-aldeg_cas04, Wuerzburg
		1, // 4-aldeg_cas05, Rothenburg
	// Geffen
		1, // 5-gefg_cas01,  Repherion
		1, // 6-gefg_cas02,  Eeyolbriggar
		1, // 7-gefg_cas03,  Yesnelph
		1, // 8-gefg_cas04,  Bergel
		1, // 9-gefg_cas05,  Mersetzdeitz
	// Payon
		1, //10-payg_cas01,  Bright Arbor
		1, //11-payg_cas02,  Scarlet Palace
		1, //12-payg_cas03,  Holy Shadow
		1, //13-payg_cas04,  Sacred Altar
		1, //14-payg_cas05,  Bamboo Grove Hill
	// Prontera
		1, //15-prtg_cas01,  Kriemhild
		1, //16-prtg_cas02,  Swanhild
		1, //17-prtg_cas03,  Fadhgridh
		1, //18-prtg_cas04,  Skoegul
		1, //19-prtg_cas05,  Gondul
	// Novice Castles
		1, //20-nguild_alde, Earth
		1, //21-nguild_gef,  Air
		1, //22-nguild_pay,  Water
		1, //23-nguild_prt,  Fire
	// Yuno
		1, //24-schg_cas01,  Himinn
		1, //25-schg_cas02,  Andlangr
		1, //26-schg_cas03,  Viblainn
		1, //27-schg_cas04,  Hljod
		1, //28-schg_cas05,  Skidbladnir
	// Rachel
		2, //29-arug_cas01,  Mardol
		2, //30-arug_cas02,  Cyr
		2, //31-arug_cas03,  Horn
		2, //32-arug_cas04,  Gefn
		2; //33-arug_cas05,  Bandis
	
	if (query_sql("SELECT ORDINAL_POSITION FROM information_schema.`COLUMNS` WHERE TABLE_SCHEMA=(SELECT DATABASE()) AND TABLE_NAME='guild' AND COLUMN_NAME='agit_points'", .@i) == 0) {
		debugmes "[Agit Points] adding column to `guild` table ...";
		query_sql "ALTER TABLE `guild` ADD `agit_points` SMALLINT(6) UNSIGNED NOT NULL DEFAULT '0'";
	}
	while(1) {
		delwaitingroom;
		waitingroom "Guild Ranking",0;
		sleep 1000;
	}
	end;

}
